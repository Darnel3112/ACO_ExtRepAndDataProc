&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	ЗарплатныйПроект = Параметры.ЗарплатныйПроект;
	МассПеймент = Параметры.МассПеймент;
	СписокВедомостей.ЗагрузитьЗначения(Параметры.СписокВедомостей.Выгрузить().ВыгрузитьКолонку("ВедомостьНаВыплатуЗарплатыВБанк"));
	
	Если МассПеймент Тогда
		
		МассивТипов = Новый Массив();
 		МассивТипов.Добавить(Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыПеречислением"));
		
 		Элементы.СписокВедомостейЗначение.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
		ЭтаФорма.Элементы.СписокВедомостейПодбор.Видимость = Ложь;
		ЭтаФорма.Элементы.СписокВедомостейПодборВедомостейВБанк.Видимость = Истина;
		ЭтаФорма.Элементы.СписокВедомостейПодборВедомостейНаСчета.Видимость = Истина;

	Иначе
		
		СписокВедомостей.ТипЗначения = Новый ОписаниеТипов("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк");
		ЭтаФорма.Элементы.СписокВедомостейПодборВедомостейВБанк.Видимость = Ложь;
		ЭтаФорма.Элементы.СписокВедомостейПодборВедомостейНаСчета.Видимость = Ложь;
		ЭтаФорма.Элементы.СписокВедомостейПодбор.Видимость = Истина;
			
	КонецЕсли;
	
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Организация"));
	НовыйМассив.Добавить(Новый СвязьПараметраВыбора("Отбор.ЗарплатныйПроект", "ЗарплатныйПроект"));
	
    ЭтаФорма.Элементы.СписокВедомостейЗначение.СвязиПараметровВыбора = Новый ФиксированныйМассив(НовыйМассив);
	ЭтаФорма.КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("СписокВедомостей", СписокВедомостей);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборВедомостейВБанк(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ФискированныеНастройкиОтбора = ПолучитьФискированныеНастройкиОтбора("Организация, ЗарплатныйПроект"); 
	Если ФискированныеНастройкиОтбора <> Неопределено Тогда
		ПараметрыФормы.Вставить("ФиксированныеНастройки", ФискированныеНастройкиОтбора);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ФормаВыбора	= ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплатыВБанк.ФормаСписка", ПараметрыФормы, ЭтаФорма.Элементы.СписокВедомостей,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	ФормаВыбора.ЗакрыватьПриВыборе = Ложь; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВедомостейОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Для каждого Ведомость Из ВыбранноеЗначение Цикл
		Если СписокВедомостей.НайтиПоЗначению(Ведомость) = Неопределено Тогда
			НоваяСтрока = СписокВедомостей.Добавить(); 
			НоваяСтрока.Значение = Ведомость;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте                                            	
Процедура ПодборВедомостейНаСчета(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ФискированныеНастройкиОтбора = ПолучитьФискированныеНастройкиОтбора("Организация"); 
	Если ФискированныеНастройкиОтбора <> Неопределено Тогда
		ПараметрыФормы.Вставить("ФиксированныеНастройки", ФискированныеНастройкиОтбора);
	КонецЕсли;

	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ФормаВыбора	= ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплатыПеречислением.ФормаСписка", ПараметрыФормы, ЭтаФорма.Элементы.СписокВедомостей,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	ФормаВыбора.ЗакрыватьПриВыборе = Ложь;

КонецПроцедуры   

Функция ПолучитьФискированныеНастройкиОтбора(ПоляОтбора)
	
	ФиксированныеНастройкиОтбора = Неопределено;
	МассивПолейОтбора = Неопределено;
	
	Если ТипЗнч(ПоляОтбора) = Тип("Строка") Тогда 
		
		МассивПолейОтбора = СтрРазделить(ПоляОтбора, ",", Ложь); 
		
		Индекс = 0;
		Пока Индекс < МассивПолейОтбора.Количество() Цикл
			Элемент = МассивПолейОтбора[Индекс];
			МассивПолейОтбора[Индекс] = СокрЛП(Элемент); 
			Индекс = Индекс + 1;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ПоляОтбора) = Тип("Массив") Тогда
		МассивПолейОтбора = ПоляОтбора;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивПолейОтбора) Тогда
		
		ФиксированныеНастройкиОтбора = Новый НастройкиКомпоновкиДанных;
		ГруппаОтборВедомостей = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ФиксированныеНастройкиОтбора.Отбор.Элементы, 
		"ГруппаОтборВедомостей", 
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		Для Каждого ПолеОтбора Из МассивПолейОтбора Цикл
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтборВедомостей, 
			ПолеОтбора,
			ЭтотОбъект[ПолеОтбора],
			ВидСравненияКомпоновкиДанных.Равно,
			ПолеОтбора,
			Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ФиксированныеНастройкиОтбора;
	
КонецФункции