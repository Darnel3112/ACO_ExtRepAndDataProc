Перем гДопДанные Экспорт;
// {{ ПрофРешение Гремицкий А.И. - 21.01.2025 - #СП-82517
Функция СведенияОВнешнейОбработке() Экспорт 
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	
	ПараметрыРегистрации.Версия = "1.3";
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Назначение.Добавить("Документ.ПриемНаРаботу");  
	ПараметрыРегистрации.Назначение.Добавить("Документ.ПриемНаРаботуСписком");
	ПараметрыРегистрации.Назначение.Добавить("Документ.КадровыйПеревод");
	ПараметрыРегистрации.Назначение.Добавить("Документ.КадровыйПереводСписком");
	ПараметрыРегистрации.Назначение.Добавить("Документ.ИндексацияЗаработка");
	ПараметрыРегистрации.Назначение.Добавить("Документ.НазначениеПлановогоНачисления");
	ПараметрыРегистрации.Назначение.Добавить("Документ.ИзменениеПлановыхНачислений");
	ПараметрыРегистрации.Назначение.Добавить("Документ.ИзменениеОплатыТруда"); 
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Протокол о назначении размера пенсии за выслугу лет";
	НоваяКоманда.Идентификатор = "ПротоколОНазначенииРазмераПенсииЗаВыслугуЛет";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовКлиентскогоМетода();
	НоваяКоманда.ПоказыватьОповещение = Истина;
	НоваяКоманда.Модификатор = "ПечатьMXL";
	
	Возврат ПараметрыРегистрации;
	
КонецФункции 

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати) Экспорт
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПротоколОНазначенииРазмераПенсииЗаВыслугуЛет",
		"Протокол о назначении размера пенсии за выслугу лет",
		СформироватьТабДок(МассивОбъектов, ОбъектыПечати));			
	
КонецПроцедуры

Функция ПолучитьФизЛицИзДокументов(МенеджерВТ, МассивСсылок)
	
	ЗапросФизЛица = Новый Запрос;
	ЗапросФизЛица.МенеджерВременныхТаблиц = МенеджерВТ;  
	ЗапросФизЛица.УстановитьПараметр("МассивСсылок", МассивСсылок);
	ЗапросФизЛица.Текст = 
		"ВЫБРАТЬ
		|	Организации.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(ПриемНаРаботу.Сотрудник) КАК СотрудникПредставление,
		|	ПриемНаРаботу.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПриемНаРаботу.Дата КАК Дата,
		|	ПриемНаРаботу.Номер КАК Номер
		|ПОМЕСТИТЬ ВТ_Сотрудники
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ПриемНаРаботу.Организация = Организации.Ссылка
		|ГДЕ
		|	ПриемНаРаботу.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(ПриемНаРаботуСпискомСотрудники.Сотрудник),
		|	ПриемНаРаботуСпискомСотрудники.ФизическоеЛицо,
		|	ПриемНаРаботуСписком.Дата,
		|	ПриемНаРаботуСписком.Номер
		|ИЗ
		|	Документ.ПриемНаРаботуСписком КАК ПриемНаРаботуСписком
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботуСписком.Сотрудники КАК ПриемНаРаботуСпискомСотрудники
		|		ПО (ПриемНаРаботуСпискомСотрудники.Ссылка = ПриемНаРаботуСписком.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ПриемНаРаботуСписком.Организация = Организации.Ссылка
		|ГДЕ
		|	ПриемНаРаботуСписком.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(КадровыйПеревод.Сотрудник),
		|	КадровыйПеревод.ФизическоеЛицо,
		|	КадровыйПеревод.Дата,
		|	КадровыйПеревод.Номер
		|ИЗ
		|	Документ.КадровыйПеревод КАК КадровыйПеревод
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО КадровыйПеревод.Организация = Организации.Ссылка
		|ГДЕ
		|	КадровыйПеревод.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(КадровыйПереводСпискомСотрудники.Сотрудник),
		|	КадровыйПереводСпискомСотрудники.ФизическоеЛицо,
		|	КадровыйПереводСписком.Дата,
		|	КадровыйПереводСписком.Номер
		|ИЗ
		|	Документ.КадровыйПереводСписком КАК КадровыйПереводСписком
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КадровыйПереводСписком.Сотрудники КАК КадровыйПереводСпискомСотрудники
		|		ПО (КадровыйПереводСпискомСотрудники.Ссылка = КадровыйПереводСписком.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО КадровыйПереводСписком.Организация = Организации.Ссылка
		|ГДЕ
		|	КадровыйПереводСписком.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(ИндексацияЗаработкаСотрудники.Сотрудник),
		|	ИндексацияЗаработкаФизическиеЛица.ФизическоеЛицо,
		|	ИндексацияЗаработка.Дата,
		|	ИндексацияЗаработка.Номер
		|ИЗ
		|	Документ.ИндексацияЗаработка КАК ИндексацияЗаработка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИндексацияЗаработка.Сотрудники КАК ИндексацияЗаработкаСотрудники
		|		ПО (ИндексацияЗаработкаСотрудники.Ссылка = ИндексацияЗаработка.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИндексацияЗаработка.ФизическиеЛица КАК ИндексацияЗаработкаФизическиеЛица
		|		ПО (ИндексацияЗаработкаФизическиеЛица.Ссылка = ИндексацияЗаработка.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ИндексацияЗаработка.Организация = Организации.Ссылка
		|ГДЕ
		|	ИндексацияЗаработка.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(НазначениеПлановогоНачисленияСотрудники.Сотрудник),
		|	НазначениеПлановогоНачисленияФизическиеЛица.ФизическоеЛицо,
		|	НазначениеПлановогоНачисления.Дата,
		|	НазначениеПлановогоНачисления.Номер
		|ИЗ
		|	Документ.НазначениеПлановогоНачисления КАК НазначениеПлановогоНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НазначениеПлановогоНачисления.Сотрудники КАК НазначениеПлановогоНачисленияСотрудники
		|		ПО (НазначениеПлановогоНачисленияСотрудники.Ссылка = НазначениеПлановогоНачисления.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НазначениеПлановогоНачисления.ФизическиеЛица КАК НазначениеПлановогоНачисленияФизическиеЛица
		|		ПО (НазначениеПлановогоНачисленияФизическиеЛица.Ссылка = НазначениеПлановогоНачисления.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО НазначениеПлановогоНачисления.Организация = Организации.Ссылка
		|ГДЕ
		|	НазначениеПлановогоНачисления.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(ИзменениеПлановыхНачисленийСотрудники.Сотрудник),
		|	ИзменениеПлановыхНачисленийФизическиеЛица.ФизическоеЛицо,
		|	ИзменениеПлановыхНачислений.Дата,
		|	ИзменениеПлановыхНачислений.Номер
		|ИЗ
		|	Документ.ИзменениеПлановыхНачислений КАК ИзменениеПлановыхНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПлановыхНачислений.Сотрудники КАК ИзменениеПлановыхНачисленийСотрудники
		|		ПО (ИзменениеПлановыхНачисленийСотрудники.Ссылка = ИзменениеПлановыхНачислений.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПлановыхНачислений.ФизическиеЛица КАК ИзменениеПлановыхНачисленийФизическиеЛица
		|		ПО (ИзменениеПлановыхНачисленийФизическиеЛица.Ссылка = ИзменениеПлановыхНачислений.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ИзменениеПлановыхНачислений.Организация = Организации.Ссылка
		|ГДЕ
		|	ИзменениеПлановыхНачислений.Ссылка В(&МассивСсылок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(ИзменениеОплатыТруда.Сотрудник),
		|	ИзменениеОплатыТруда.ФизическоеЛицо,
		|	ИзменениеОплатыТруда.Дата,
		|	ИзменениеОплатыТруда.Номер
		|ИЗ
		|	Документ.ИзменениеОплатыТруда КАК ИзменениеОплатыТруда
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ИзменениеОплатыТруда.Организация = Организации.Ссылка
		|ГДЕ
		|	ИзменениеОплатыТруда.Ссылка В(&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТ_Сотрудники КАК ВТ_Сотрудники";
	
	МассивФизЛиц = ЗапросФизЛица.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");  	
    Возврат МассивФизЛиц; 
	
КонецФункции 

Процедура СоздатьВТ_ДанныеФизЛиц(МенеджерВТ, СтажФизЛиц)

	Запрос = Новый Запрос; 
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;        
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("СтажФизЛиц", СтажФизЛиц);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтажФизЛиц.ОбщийСтажЛет КАК ОбщийСтажЛет,
		|	СтажФизЛиц.ОбщийСтажМесяцев КАК ОбщийСтажМесяцев,
		|	СтажФизЛиц.ОбщийСтажДней КАК ОбщийСтажДней,
		|	СтажФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТ_СтажФизЛиц
		|ИЗ
		|	&СтажФизЛиц КАК СтажФизЛиц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СтажФизЛиц.ОбщийСтажЛет КАК ОбщийСтажЛет,
		|	ВТ_СтажФизЛиц.ОбщийСтажМесяцев КАК ОбщийСтажМесяцев,
		|	ВТ_СтажФизЛиц.ОбщийСтажДней КАК ОбщийСтажДней,
		|	ВТ_СтажФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ФизическиеЛица.ДатаРождения КАК ДатаРождения
		|ПОМЕСТИТЬ ВТ_КадровыеДанныеФизЛиц
		|ИЗ
		|	ВТ_СтажФизЛиц КАК ВТ_СтажФизЛиц
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО ВТ_СтажФизЛиц.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Период КАК ДатаИзменения,
		|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Показатель.Идентификатор КАК ПоказательУИД,
		|	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Значение КАК Значение
		|ПОМЕСТИТЬ ВТ_ПоказателиЗПФизЛиц
		|ИЗ
		|	РегистрСведений.ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудников.СрезПоследних(
		|			&Дата,
		|			ФизическоеЛицо В
		|				(ВЫБРАТЬ
		|					ВТ_СтажФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|				ИЗ
		|					ВТ_СтажФизЛиц КАК ВТ_СтажФизЛиц)) КАК ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПроцентыСевернойНадбавкиФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПроцентыСевернойНадбавкиФизическихЛицСрезПоследних.ПроцентСевернойНадбавки КАК Значение,
		|	ПроцентыСевернойНадбавкиФизическихЛицСрезПоследних.Период КАК ДатаИзменения
		|ПОМЕСТИТЬ ВТ_СевернаяНадбавка
		|ИЗ
		|	РегистрСведений.ПроцентыСевернойНадбавкиФизическихЛиц.СрезПоследних(
		|			,
		|			ФизическоеЛицо В
		|				(ВЫБРАТЬ
		|					ВТ_Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|				ИЗ
		|					ВТ_Сотрудники КАК ВТ_Сотрудники)) КАК ПроцентыСевернойНадбавкиФизическихЛицСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПоказателиЗПФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТ_ПоказателиЗПФизЛиц.ДатаИзменения КАК ДатаИзменения,
		|	ВТ_ПоказателиЗПФизЛиц.ПоказательУИД КАК ПоказательУИД,
		|	ВТ_ПоказателиЗПФизЛиц.Значение КАК Значение
		|ПОМЕСТИТЬ ВТ_ПоказателиФизЛиц
		|ИЗ
		|	ВТ_ПоказателиЗПФизЛиц КАК ВТ_ПоказателиЗПФизЛиц
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_СевернаяНадбавка.ФизическоеЛицо,
		|	ВТ_СевернаяНадбавка.ДатаИзменения,
		|	""СевернаяНадбавка"",
		|	ВТ_СевернаяНадбавка.Значение
		|ИЗ
		|	ВТ_СевернаяНадбавка КАК ВТ_СевернаяНадбавка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СтажФизЛиц";   
		
    Запрос.Выполнить();
	
КонецПроцедуры 

Процедура ЗаполнитьСтаж(Область, Стаж)
	Если ЗначениеЗаполнено(Стаж.ОбщийСтажЛет) Тогда
			
		СтажЛет = СтрокаСЧислом(
			"; %1 год; ; %1 года; %1 лет; %1 года", 
			Стаж.ОбщийСтажЛет, 
			ВидЧисловогоЗначения.Количественное, 
			"Л=ru; ЧН=0"
		);   
		СтажМесяцев = СтрокаСЧислом(
			"; %1 месяц; ; %1 месяца; %1 месяцев; %1 месяца", 
			Стаж.ОбщийСтажМесяцев, 
			ВидЧисловогоЗначения.Количественное, 
			"Л=ru; ЧН=0"
		);    
		СтажДней = СтрокаСЧислом(
			"; %1 день; ; %1 дня; %1 дней; %1 дня", 
			Стаж.ОбщийСтажДней, 
			ВидЧисловогоЗначения.Количественное, 
			"Л=ru; ЧН=0"
		);              
		Область.Параметры.Стаж = СтрШаблон("%1 %2 %3", СтажЛет, СтажМесяцев, СтажДней);
	КонецЕсли;
КонецПроцедуры

Функция СформироватьТабДок(МассивОбъектов, ОбъектыПечати) Экспорт 
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Макет = ПолучитьМакет("ПФ_MXL_Макет");
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	МассивФизЛиц = ПолучитьФизЛицИзДокументов(МВТ, МассивОбъектов);
	
	ТЗКадровыеДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(ЛОЖЬ,
		МассивФизЛиц,
		"ОбщийСтажЛет,ОбщийСтажМесяцев,ОбщийСтажДней",
		ТекущаяДатаСеанса());            
	
	СоздатьВТ_ДанныеФизЛиц(МВТ, ТЗКадровыеДанныеФизЛиц);  
	
	РеквизитыРегистраторов = МВТ.Таблицы.Найти("ВТ_Сотрудники").ПолучитьДанные().Выгрузить();
	
	СтажДРФизЛиц = МВТ.Таблицы.Найти("ВТ_КадровыеДанныеФизЛиц").ПолучитьДанные().Выгрузить(); 
	
	ПоказателиФизЛиц = МВТ.Таблицы.Найти("ВТ_ПоказателиФизЛиц").ПолучитьДанные().Выгрузить(); 
	
	Для каждого ФизЛицо Из МассивФизЛиц Цикл
		
		ОбластьПротокол = Макет.ПолучитьОбласть("Протокол");
		
		ОтборПоФизЛицу = Новый Структура("ФизическоеЛицо", ФизЛицо);   
		
		РеквизитыРегистратора = РеквизитыРегистраторов.Найти(ФизЛицо, "ФизическоеЛицо");
		
		СтажДРФизЛица = СтажДРФизЛиц.Найти(ФизЛицо, "ФизическоеЛицо");
			 
		КоэфициентЗаВыслугуЛет = 2.8;     
		Если СтажДРФизЛица.ОбщийСтажЛет > 30 Тогда
			КоэфициентЗаВыслугуЛет = 2.8 + 0.1 * (СтажДРФизЛица.ОбщийСтажЛет - 30);
		КонецЕсли;

		ТЗПоказателиФизЛица = ПоказателиФизЛиц.Скопировать(ОтборПоФизЛицу, "ПоказательУИД, Значение, ДатаИзменения");
		
		ПоказателиФизЛица = Новый Структура("ОкладПоДолжности, КоэффициентЕжемесячнойНадбавкиЗаКлассныйЧин, 
			|РайонныйКоффициент,СевернаяНадбавка,
			|ПроцентОтСреднемесячногоЗаработка,
			|РазмерСтраховойТрудовойПенсии",
			0, 0, 1, 0, 0, 0);    
		
		Для каждого СтрокаПоказателя Из ТЗПоказателиФизЛица Цикл    
			Если ЗначениеЗаполнено(СтрокаПоказателя.Значение) Тогда      
				ПоказателиФизЛица.Вставить(СтрокаПоказателя.ПоказательУИД, СтрокаПоказателя.Значение); 	
			КонецЕсли;                  
		КонецЦикла;  
		
		ИтогоДоплата = СтрШаблон(
			"(%1 * %2 + %3 * (%4 + %5) * %6) * %7 - %8",
			ПоказателиФизЛица.ОкладПоДолжности,
			ПоказателиФизЛица.КоэффициентЕжемесячнойНадбавкиЗаКлассныйЧин/100,
			ПоказателиФизЛица.ОкладПоДолжности,  
			ПоказателиФизЛица.РайонныйКоффициент,
			ПоказателиФизЛица.СевернаяНадбавка/100,
			КоэфициентЗаВыслугуЛет, 
			ПоказателиФизЛица.ПроцентОтСреднемесячногоЗаработка/100,
			ПоказателиФизЛица.РазмерСтраховойТрудовойПенсии
		);      
		
		Итог = 
			(ПоказателиФизЛица.ОкладПоДолжности * 
			ПоказателиФизЛица.КоэффициентЕжемесячнойНадбавкиЗаКлассныйЧин/100 +
			ПоказателиФизЛица.ОкладПоДолжности *  
			(ПоказателиФизЛица.РайонныйКоффициент +
			ПоказателиФизЛица.СевернаяНадбавка/100) * 
			КоэфициентЗаВыслугуЛет) *  
			ПоказателиФизЛица.ПроцентОтСреднемесячногоЗаработка/100 -
			ПоказателиФизЛица.РазмерСтраховойТрудовойПенсии;
			
		Итог = Окр(Итог, 2, РежимОкругления.Окр15как10);      
		Итог = ?(Итог < 7000, 7000, Итог);         
		
		ИтогоДоплата = ИтогоДоплата + " = " + Строка(Итог);
		
		ОбластьПротокол.Параметры.Заполнить(РеквизитыРегистратора); 
		ЗаполнитьСтаж(ОбластьПротокол, СтажДРФизЛица);	
		ОбластьПротокол.Параметры.Заполнить(ПоказателиФизЛица);
		ОбластьПротокол.Параметры.Дата 					 = Формат(РеквизитыРегистратора.Дата, "ДФ=dd.MM.yyyy");
		ОбластьПротокол.Параметры.ДатаРождения 			 = Формат(СтажДРФизЛица.ДатаРождения, "ДФ=dd.MM.yyyy");
		ОбластьПротокол.Параметры.КоэфициентЗаВыслугуЛет = КоэфициентЗаВыслугуЛет;
		ОбластьПротокол.Параметры.ИтогоДоплата 		 	 = Итог;                                                 
		ОбластьПротокол.Параметры.РасчетИтого 			 = ИтогоДоплата;

		ДокументРезультат.Вывести(ОбластьПротокол);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции  // ПрофРешение Гремицкий А.И. - 21.01.2025 - #СП-82517 }}
